#### Задача 1
*Есть регистр Продажи. Измерение - Клиент, Товар. Ресурсы - Количество
Запросом получить всех клиентов, кто купил товар А, но не купил товар B*

```SQL
&НаСервере
Процедура ПолучитьНаСервере()   
	КупленныйТовар = Отчет.КупленныйТовар;
	ИсключающийТовар = Отчет.ИсключающийТовар;
	
	ЗапросДанных = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ Продажи.Клиент КАК Клиенты
				| ИЗ
				| РегистрСведений.Продажи КАК Продажи
				| ГДЕ
				| (Продажи.Товар = &КупленныйТовар)
				| И 
				| (Продажи.Клиент НЕ В(
				| ВЫБРАТЬ Продажи.Клиент 
				| ИЗ
				| РегистрСведений.Продажи КАК Продажи
				| ГДЕ
				| Продажи.Товар = &ИсключающийТовар))";
	ЗапросДанных.Текст = ТекстЗапроса;
	ЗапросДанных.УстановитьПараметр("КупленныйТовар", КупленныйТовар);
	ЗапросДанных.УстановитьПараметр("ИсключающийТовар", ИсключающийТовар);
	ПолученныеДанные = ЗапросДанных.Выполнить().Выгрузить(); 
	
	Для Каждого Элемент Из ПолученныеДанные Цикл
		Строка = Отчет.Клиент.Добавить();
		Строка.Покупатель = Элемент.Клиенты;
	КонецЦикла;
			
КонецПроцедуры

```

Результат:

![1_Запрос](https://user-images.githubusercontent.com/36735568/190198246-ecff6ad6-c239-4a0b-9536-61197803af26.png)

Список продаж:

![1_Продажи](https://user-images.githubusercontent.com/36735568/190198353-b26394a7-293d-4e70-adf2-c897ae77ea3c.png)





#### Задача 2
*Есть таблица значений с полями Контрагент и Сумма. Напишите процедуру, которая удалит из таблицы все строки, с суммой меньше 100 рублей*

```js
Функция СоздатьТаблицу()
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Контрагент");
	Таблица.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 2)));   
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	Для Число = 0 по 100 цикл
		Строка = Таблица.Добавить();
		Строка.Контрагент = Символ(ГСЧ.СлучайноеЧисло(65,90))+Символ(ГСЧ.СлучайноеЧисло(65,90))+Символ(ГСЧ.СлучайноеЧисло(65,90));
		Строка.Сумма = ГСЧ.СлучайноеЧисло(1,1000);
	КонецЦикла;
	Возврат Таблица;
КонецФункции


Процедура ИзменитьТаблицу()   
	
	Таблица = СоздатьТаблицу(); 
	
	Построитель = Новый ПостроительЗапроса();
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(Таблица);
	
	Отбор = Построитель.Отбор.Добавить("Сумма");
	Отбор.ВидСравнения = ВидСравнения.БольшеИлиРавно;
	Отбор.Значение = 100;
	Отбор.Использование = Истина;
	
	Построитель.Выполнить();
	НоваяТаблица = Построитель.Результат.Выгрузить();
	Отчет.ТабличнаяЧасть.Загрузить(НоваяТаблица);
	//Отчет.ТабличнаяЧасть.Сортировать("Сумма");
			
КонецПроцедуры

```
Результат:


![2](https://user-images.githubusercontent.com/36735568/190459863-83bca8c7-c317-499e-af8d-6a38ff0ec1d6.png)



#### Задача 3
*В конфигурации есть регистр сведений Производственный календарь. Измерение - Дата, ресурс - Рабочий день(булево), Истина - если день рабочий, иначе  - Ложь
Реализовать функцию, которая прибавляет X рабочих Дней к исходной дате.*

```js

&НаСервере
Функция ДобавитьДнейНаСервере()
	Дата = Отчет.ИсходнаяДата;
	РабочихДней = Отчет.КоличествоДней;
	ЧислоЦикла = 0; 
	Пока ЧислоЦикла < РабочихДней Цикл
		РабочийДень = РегистрыСведений.ПроизводственныйКалендарь.Получить(Новый Структура("Дата", Дата)).РабочийДень;
		Если РабочийДень Тогда
			ЧислоЦикла = ЧислоЦикла+1;
		КонецЕсли; 
		Дата = КонецДня(Дата) + 86400;
	КонецЦикла;
	
	Возврат Дата;
		
КонецФункции

```
Результат:

![3proc](https://user-images.githubusercontent.com/36735568/190375524-22b38df9-8880-46d0-8e99-9ac49ef7e358.png)

Календарь:

![3calend](https://user-images.githubusercontent.com/36735568/190375591-df3fc9d9-ed13-4851-a24d-7858b44d272b.png)





#### Задача 4.
*Напишете текст запроса (одним запросом) для получения курса валюты из периодического (периодичность день) регистра сведений «Курсы валют» для документов «Поступление» на дату каждого документа.
Регистр сведений «Курсы валют»:*

```SQL
&НаСервере
Процедура ЗапросНаСервере()
	ЗапросДанных = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
				| Поступление.Номер КАК ДокументПоступления,
				| Поступление.Валюта КАК Валюта,
				| Поступление.Дата КАК ДатаДокумента,
				| КурсыВалют.Курс КАК КурсВалютыНаДатуДокумента
				| ИЗ
				| Документ.Поступление КАК Поступление
				| ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют 
				| ПО Поступление.Валюта = КурсыВалют.Валюта
				| И НАЧАЛОПЕРИОДА(Поступление.Дата, ДЕНЬ) = КурсыВалют.Период ";
				
	ЗапросДанных.Текст = ТекстЗапроса;
	ПолученныеДанные = ЗапросДанных.Выполнить().Выгрузить();
	
	Отчет.ТабличнаяЧасть1.Загрузить(ПолученныеДанные);	
	
КонецПроцедуры

```
Результат:

![4res](https://user-images.githubusercontent.com/36735568/190412223-298b513a-a7d9-44ae-ba98-7c713c6ab967.png)

Курс:

![4course](https://user-images.githubusercontent.com/36735568/190412291-d742ca0e-d28c-4bd7-8621-540dd9baa78c.png)

Поступления:

![4pos](https://user-images.githubusercontent.com/36735568/190412352-f37dd93d-f43f-4bee-90f7-3bf56745f356.png)



#### Задача 5
*Получить продажи помесячно нарастающим итогом.
Например. Контрагент “Рога и копыта” купил в январе на 100 рублей, в феврале на 50 рублей, в марте ничего не купил , в апреле на  100  рублей*
```js
&НаСервере
Процедура ПолучитьНаСервере()
	Контрагент = Отчет.Контрагент;
	Дата = Отчет.Год;
	
	ЗапросДанных = Новый Запрос;
	ТекстЗапроса = "
			| ВЫБРАТЬ   
			| 	Продажи.Контрагент КАК Контрагент,
			|	НАЧАЛОПЕРИОДА(Продажи.Дата, МЕСЯЦ) КАК Период,
			| 	СУММА(Продажи.Сумма) КАК Сумма
			| ПОМЕСТИТЬ ТаблицаПродаж
			| ИЗ
			|	РегистрСведений.Продажа КАК Продажи
			| ГДЕ
			|	НАЧАЛОПЕРИОДА(Продажи.Дата, ГОД) = НАЧАЛОПЕРИОДА(&Дата, ГОД) 
			| И
			|	Продажи.Контрагент = &Контрагент
			| СГРУППИРОВАТЬ ПО
			|	Продажи.Контрагент,
			|	НАЧАЛОПЕРИОДА(Продажи.Дата, МЕСЯЦ)
			|;
			| ВЫБРАТЬ
			| 	НАЧАЛОПЕРИОДА(Календарь.Дата, МЕСЯЦ) КАК Период
			| ПОМЕСТИТЬ Календарь
			| ИЗ
			|	РегистрСведений.ПроизводственныйКалендарь КАК Календарь
			| ГДЕ
			|	НАЧАЛОПЕРИОДА(Календарь.Дата, ГОД) = НАЧАЛОПЕРИОДА(&Дата, ГОД)
			| СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(Календарь.Дата, МЕСЯЦ)
			|;
			| ВЫБРАТЬ
			|	Календарь.Период КАК Период,
			|	ТаблицаПродаж.Контрагент КАК Контрагент,
//			|	ТаблицаПродаж.Сумма КАК Сумма,
			|	СУММА(Накопитель.Сумма) КАК СуммаНарастающимИтогомСНачалаГода
			| ИЗ
			|	Календарь КАК Календарь
			| 	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПродаж КАК ТаблицаПродаж
			|	ПО Календарь.Период = ТаблицаПродаж.Период
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПродаж КАК Накопитель
			|		ПО Календарь.Период >= Накопитель.Период	
			| СГРУППИРОВАТЬ ПО
			|	Календарь.Период,
			|	ТаблицаПродаж.Контрагент
//			|	ТаблицаПродаж.Сумма
			| УПОРЯДОЧИТЬ ПО
			|	Календарь.Период		
			| ";
				
	ЗапросДанных.Текст = ТекстЗапроса;
	ЗапросДанных.УстановитьПараметр("Дата", Дата);
	ЗапросДанных.УстановитьПараметр("Контрагент", Контрагент);   
	ПолученныеДанные = ЗапросДанных.Выполнить().Выгрузить();	
	
	Отчет.ТабличнаяЧасть.Загрузить(ПолученныеДанные);
	
КонецПроцедуры

```

Результат:

![5_yes](https://user-images.githubusercontent.com/36735568/190698373-89067600-b16b-420c-8554-1b4f732cdc11.png)

С суммой продаж

![5_summ](https://user-images.githubusercontent.com/36735568/190698752-8dacdf90-3a48-4a89-912d-60354ef1c501.png)

Таблица продаж

![5_torg](https://user-images.githubusercontent.com/36735568/190698568-d04918e8-3273-4617-aef5-d6787f8447b1.png)

#### Задача 6
*В конфигурации, которую вы создаете с нуля, необходимо иметь возможность учитывать дебиторскую задолженность по срокам долга.
Документы, которые уже есть в конфигурации:
Реализация  - регистрирует продажу
Приходный кассовый ордер - регистрирует оплату от клиента
Нужно получать отчет в виде:
Документ Реализации / Сумма / Сумма оплачено / Сумма просроченная/ Срок просрочки в днях.*

Сначала я создам справочники "Контрагенты" и "Номенклатура".

Документ "Реализация" будет иметь Реквизиты:
* Контрагент
* Сумма
* Оплачен - булево
* СуммаПоступлений - здесь будет храниться сумма поступивших оплат, если оплаты будут приходить частями.

В табличной части будет две таблицы, в первую будут добавляться товары, во вторую документы оплаты автоматически.


![6sel_sel](https://user-images.githubusercontent.com/36735568/190869537-afb0b113-6f8c-4a20-8f95-ca71b2de093f.png) 
![6sel_pos](https://user-images.githubusercontent.com/36735568/190869546-fdd06d1d-e5a9-4979-bbd8-9d8e75e1f3a0.png)


Предполагается, что для приходного кассового ордера должен быть документ основание. Соответсвенно, я добавил в его реквизит.

В модуль объекта ПриходныйКассовыйМодуль добавил следующий код:
```js
&НаСервере
Процедура ПриЗаписи(Отказ) 

	Реализация = ДокументОснование.ПолучитьОбъект();
	РеализацияПоступление = Реализация.Поступления.Добавить(); // Добавляем в табличную часть документа реализации этот ПКО
	РеализацияПоступление.ДокументОплаты = ЭтотОбъект.Ссылка; 
	
	Сумма = 0;						
	Для Каждого ПКО ИЗ Реализация.Поступления Цикл		  // Получаем сумму из всех ПКО по этой реализации
		Сумма = Сумма + ПКО.ДокументОплаты.Сумма;
	КонецЦикла; 
	
	Если Реализация.Сумма = Сумма Тогда
		Реализация.Оплачен = Истина;			  // Если сумма всех ПКО равна сумме документа, документ помечается оплаченным. 
	КонецЕсли;    
	
	Если Сумма > Реализация.Сумма Тогда			  // Отказ, если сумма всех ПКО, включая текущий выше суммы Реализации 
		Отказ = Истина;
	КонецЕсли;
	
	Реализация.СуммаПоступлений = Сумма;
	
	Реализация.Записать(); 
		
КонецПроцедуры

```
